/*
 * Copyright (c) 2022 Balanced.network.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import network.balanced.score.dependencies.Dependencies

version = '0.1.0'

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    compileOnly Dependencies.javaeeApi
    implementation project(':score-lib')

    annotationProcessor Dependencies.javaeeScoreClient
    compileOnly Dependencies.javaeeScoreClient  

    implementation Dependencies.javaeeScorex

    testImplementation Dependencies.javaeeUnitTest
    testImplementation Dependencies.junitJupiter
    testRuntimeOnly Dependencies.junitJupiterEngine
    testImplementation Dependencies.mockitoCore
    testImplementation project(':test-lib')
    testImplementation 'org.mockito:mockito-inline:2.13.0'

    intTestImplementation project(':score-client')
    intTestImplementation Dependencies.minimalJson
    intTestAnnotationProcessor Dependencies.javaeeScoreClient
    intTestImplementation Dependencies.iconSdk
    intTestImplementation Dependencies.jacksonDatabind
}

optimizedJar {
    mainClassName = 'network.balanced.score.core.rewards.RewardsImpl'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }

    enableDebug = false
}

deployJar {
    endpoints {
        sejong {
            uri = 'https://sejong.net.solidwallet.io/api/v3'
            nid = 0x53
            to = "cx75256fadf232ad1124d9c6cd70c9b1ec122a0f47"
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
        mainnet {
            uri = 'https://ctz.solidwallet.io/api/v3'
            nid = 0x1
            to = "cx793970c9ec84eb0dcb6164965c74fe678474d7c7"
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {}
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    finalizedBy jacocoTestReport 
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}


task integrationTest(type: Test) {
    useJUnitPlatform()

    rootProject.allprojects {
        if(it.getTasks().findByName('optimizedJar')) {
            dependsOn(it.getTasks().getByName('optimizedJar'))
        }
    }

    options {
        testLogging.showStandardStreams = true
        description = 'Runs integration tests.'
        group = 'verification'

        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath

        systemProperty "python", new File(project(':test-lib').projectDir, 'python-contracts/rewards.zip')
        systemProperty "java", optimizedJar.outputJarName        
    }

}