/*
 * Copyright (c) 2022-2022 Balanced.network.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import groovy.swing.SwingBuilder
import network.balanced.score.dependencies.Dependencies


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath Dependencies.javaeePlugin
    }
}

def scoreIntegrationTest = new HashMap<String, String>()

task buildContracts(type: Exec) {
    workingDir "$projectDir/"
    commandLine './gradlew', 'optimizedJar'
}


apply plugin: 'deploy-contracts'
deployBalancedContracts  {
    envs {
        local {
            env = "local"
        }
        berlin {
            env = "berlin"
            configFile = "contracts-sample.json"
            keystore = rootProject.findProperty('keystoreName') ?: ''
            password = rootProject.findProperty('keystorePass') ?: ''
        }
        sejong {
            env = "sejong"
            configFile = "contracts-sample.json"
            keystore = rootProject.findProperty('keystoreName') ?: ''
            password = rootProject.findProperty('keystorePass') ?: ''
        }
    }
}

project.tasks.getByName("deployContractsToBerlin").dependsOn(project.tasks.getByName("buildContracts"))
project.tasks.getByName("deployContractsToSejong").dependsOn(project.tasks.getByName("buildContracts"))
project.tasks.getByName("deployContractsToLocal").dependsOn(project.tasks.getByName("buildContracts"))


executeBalancedActions {
    contractAddressFile = "sample.json"
    propertiesFile = "sample.properties"
    actionsFile = "actions-sample.json"
    keystore = rootProject.findProperty('keystoreName') ?: ''
    password = rootProject.findProperty('keystorePass') ?: ''
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'foundation.icon.javaee'

    sourceSets {
        intTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            java {
            }
        }

    }

    configurations {
        intTestImplementation.extendsFrom testImplementation
        intTestAnnotationProcessor.extendsFrom testAnnotationProcessor
        intTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    // need to add this option to retrieve formal parameter names
    compileJava {
        options.compilerArgs += ['-parameters']
    }

    scoreIntegrationTest.put('env.props', new File(project(':test-lib').projectDir, 'conf/env.props'));

    //remove as more contracts are added to master
    scoreIntegrationTest.put('Governance', new File(project(':test-lib').projectDir, 'python-contracts/governance.zip'));
    scoreIntegrationTest.put('Baln', new File(project(':test-lib').projectDir, 'python-contracts/baln.zip'));
    scoreIntegrationTest.put('Bwt', new File(project(':test-lib').projectDir, 'python-contracts/bwt.zip'));
    scoreIntegrationTest.put('Dex', new File(project(':test-lib').projectDir, 'python-contracts/dex.zip'));
    scoreIntegrationTest.put('Feehandler', new File(project(':test-lib').projectDir, 'python-contracts/feehandler.zip'));
    scoreIntegrationTest.put('Loans', new File(project(':test-lib').projectDir, 'python-contracts/loans.zip'));
    // scoreIntegrationTest.put('rebalancing', new File(project(':test-lib').projectDir, 'python-contracts/rebalancing.zip'));
    scoreIntegrationTest.put('Rewards', new File(project(':test-lib').projectDir, 'python-contracts/rewards.zip'));
    scoreIntegrationTest.put('Sicx', new File(project(':test-lib').projectDir, 'python-contracts/sicx.zip'));
    scoreIntegrationTest.put('Bnusd', new File(project(':test-lib').projectDir, 'python-contracts/bnusd.zip'));
    scoreIntegrationTest.put('daofund', new File(project(':test-lib').projectDir, 'python-contracts/daofund.zip'));
//    scoreIntegrationTest.put('Dividends', new File(project(':test-lib').projectDir, 'python-contracts/dividends.zip'));
    scoreIntegrationTest.put('Oracle', new File(project(':test-lib').projectDir, 'python-contracts/oracle.zip'));
    scoreIntegrationTest.put('Reserve', new File(project(':test-lib').projectDir, 'python-contracts/reserve.zip'));
    scoreIntegrationTest.put('Router', new File(project(':test-lib').projectDir, 'python-contracts/router.zip'));
    scoreIntegrationTest.put('Staking', new File(project(':test-lib').projectDir, 'python-contracts/staking.zip'));

    afterEvaluate { project ->
        scoreIntegrationTest.put(project.name, project.getTasks().getByName("optimizedJar").outputJarName)
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().any { task ->
        if (task.name == "integrationTest") {
            scoreIntegrationTest.each { key, val -> task.systemProperty key, val }
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (rootProject.hasProperty('keystorePass')) {
        return
    }
    def hasDeployTask = taskGraph.getAllTasks().any { it ->
        return it.name == "deployToMainnet"
    }

    if (hasDeployTask) {
        def pass = ''
        if (System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Keystore password',
                        alwaysOnTop: true,
                        resizable: false,
                        locationRelativeTo: null,
                        pack: true,
                        show: true
                ) {
                    vbox {
                        hbox {
                            label(text: 'Please enter key passphrase: ')
                        }
                        hbox {
                            input = passwordField(columns: 20, id: 'password')
                        }
                        hbox {
                            button(defaultButton: true, text: 'OK', actionPerformed: {
                                pass = input.password
                                dispose()
                            })
                        }
                    }
                }
            }
        } else {
            pass = System.console().readPassword("\nPlease enter key passphrase: ")
            pass = new String(pass)
        }

        if (pass.size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }
        rootProject.setProperty('keystorePass', pass)

    }
}